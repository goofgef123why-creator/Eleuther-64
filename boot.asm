[BITS 32]
SECTION .multiboot
ALIGN 4
DD 0x1BADB002
DD 0
DD -(0x1BADB002)
SECTION .text
GLOBAL _start
EXTERN KERNEL_ENTRY
_start:
    CLI
    MOV ESP, STACKTOP
    CMP EAX, 0x2BADB002
    JNE HALT
    MOV AL, 0xFF
    OUT 0x21, AL
    OUT 0xA1, AL
    MOV EAX, CR4
    OR EAX, 1 << 5
    MOV CR4, EAX
    MOV EAX, PML4
    MOV CR3, EAX
    MOV ECX, 0xC0000080
    RDMSR
    OR EAX, 1 << 8
    WRMSR
    LGDT [GDT64DESC]
    MOV EAX, CR0

    OR EAX, 1 << 31
    MOV CR0, EAX

    JMP 0x08:LONGMODEENTRY
HALT:
    HLT
    JMP HALT
[BITS 64]
LONGMODEENTRY:
    MOV AX, 0x10
    MOV DS, AX
    MOV ES, AX
    MOV SS, AX
    MOV RSP, STACKTOP
    MOV RAX, KERNEL_ENTRY
    JMP RAX
STOP:
    HLT
    JMP STOP
SECTION .rodata
GDT64:
    DQ 0
    DQ 0x00AF9A000000FFFF
    DQ 0x00AF92000000FFFF
GDT64END:
GDT64DESC:
    DW GDT64END - GDT64 - 1
    DQ GDT64
SECTION .data
ALIGN 4096
PML4:
    DQ PDPT + 0x3
    TIMES 511 DQ 0
ALIGN 4096
PDPT:
    DQ PD + 0x3
    TIMES 511 DQ 0
ALIGN 4096
PD:
    DQ 0x00000000 + 0x83
    DQ 0x00200000 + 0x83
    DQ 0x00400000 + 0x83
    DQ 0x00600000 + 0x83
    DQ 0x00800000 + 0x83
    DQ 0x00A00000 + 0x83
    DQ 0x00C00000 + 0x83
    DQ 0x00E00000 + 0x83
    TIMES 504 DQ 0
SECTION .bss
ALIGN 16
STACK:
    RESB 4096
STACKTOP: